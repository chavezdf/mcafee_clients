@ECHO OFF
COLOR 1f

REM	-------------------------------------------------------------------------------
REM	+CHANGE LOG
REM	11/23/2011: Created
REM	12/06/2011: Added Checkversions and install check
REM	-------------------------------------------------------------------------------	

REM	-------------------------------------------------------------------------------
REM		AUTHOR: Levon Becker 
REM		TITLE: Uninstall-VSEClient 
REM		PURPOSE: Uninstall McAfee VirusScan Enterprise Client
REM		VERSION: 1.1
REM	-------------------------------------------------------------------------------

CLS
CALL %CHECKVERSIONS%

REM CHECK IF VSE INSTALLED
IF NOT "%VSEVER%"=="Not Installed" GOTO VSEUNINSTALL
ECHO.
ECHO	[VSE CLIENT NOT INSTALLED]
ECHO.
ECHO.	>> %MCAFEELOG%
ECHO	[VSE CLIENT NOT INSTALLED] (%DATE% %TIME%) >> %MCAFEELOG%
CALL %SUBSCRIPTS%\WAIT.cmd 2
GOTO END

:VSEUNINSTALL
REM REM WRITE LOG BEFORE SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	BEFORE VSE CLIENT UNINSTALL >> %MCAFEELOG%
CALL "%SUBSCRIPTS%\WriteLog-VSESUM.cmd" %MCAFEELOG%

ECHO.	>> %MCAFEELOG%
ECHO	[ATTEMPTING VSE CLIENT UNINSTALL] (%DATE% %TIME%) >> %MCAFEELOG%

ECHO.
ECHO	[UNINSTALLING VSE CLIENT]
ECHO.
ECHO	Please Wait...
REM UNINSTALL VSE
REM VirusScan Enterprise 8.8 
msiexec /x {CE15D1B6-19B6-4D4D-8F43-CF5D2C3356FF} REMOVE=ALL REBOOT=R /q

REM VirusScan Enterprise 8.7i 
msiexec /x {147BCE03-C0F1-4C9F-8157-6A89B6D2D973} REMOVE=ALL REBOOT=R /q
 
REM VirusScan Enterprise 8.5i 
msiexec.exe /x {35C03C04-3F1F-42C2-A989-A757EE691F65} REMOVE=ALL REBOOT=R /q

REM VirusScan Enterprise 8.0i 
msiexec.exe /x {5DF3D1BB-894E-4DCD-8275-159AC9829B43} REMOVE=ALL REBOOT=R /q

REM CHECK SUCCESS
CALL %CHECKVERSIONS%
IF "%VSEVER%"=="Not Installed" GOTO COMPLETED
GOTO ERROR
REM IF EXIST "%VSEPATH%\scan32.exe" GOTO ERROR

:COMPLETED

REM WRITE LOG AFTER SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	AFTER VSE CLIENT UNINSTALL >> %MCAFEELOG%
CALL "%SUBSCRIPTS%\WriteLog-VSESUM.cmd" %MCAFEELOG%

ECHO.	>> %MCAFEELOG%
ECHO	[VSE CLIENT UNINSTALLED SUCCESSFULLY] >> %MCAFEELOG%

CLS
ECHO.
ECHO	[VSE CLIENT UNINSTALLED SUCCESSFULLY]
ECHO.
SET ERROR=0
REM Wait 2 seconds
CALL %SUBSCRIPTS%\WAIT.cmd 2
GOTO END

:ERROR
COLOR 1c
CLS
ECHO.
ECHO	ERROR: [VSE CLIENT UNINSTALL FAILED]
ECHO.
SET ERROR=1
REM WRITE LOG FAILED SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	ERROR: [VSE CLIENT UNINSTALL FAILED] (%DATE% %TIME%) >> %MCAFEELOG%
PAUSE
GOTO END

:END