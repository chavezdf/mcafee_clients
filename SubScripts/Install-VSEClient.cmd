@ECHO OFF
COLOR 1f

REM	-------------------------------------------------------------------------------
REM	+CHANGE LOG
REM	11/23/2011: Created
REM 12/06/2011: Added Logging, Removed the MCTEMP variable and create dir
REM				Added install check at start
REM	-------------------------------------------------------------------------------	

REM	-------------------------------------------------------------------------------
REM		AUTHOR: Levon Becker 
REM		TITLE: Install-VSEClient 
REM		PURPOSE: Install Latest McAfee VirusScan Enterprise Client
REM		VERSION: 1.1
REM	-------------------------------------------------------------------------------


CLS
ECHO.	>> %MCAFEELOG%
ECHO	[ATTEMPTING VSE CLIENT %CURVSEVER% INSTALL] (%DATE% %TIME%) >> %MCAFEELOG%
CALL %CHECKVERSIONS%
REM CHECK IF VSE INSTALLED
IF "%VSEVER%"=="Not Installed" GOTO VSEINSTALL
ECHO.
ECHO	[VSE CLIENT ALREADY INSTALLED]
ECHO.
ECHO.	>> %MCAFEELOG%
ECHO	[VSE CLIENT ALREADY INSTALLED] (%DATE% %TIME%) >> %MCAFEELOG%
CALL %SUBSCRIPTS%\WAIT.cmd 2
GOTO END

:VSEINSTALL
CLS
ECHO.
ECHO	[COPYING VSE CLIENT INSTALL FILES LOCAL]
ECHO.

REM CREATE TEMP FOLDER
SET VSETEMP=%McAfeeClientsTemp%\VSE
SET VSEVERTEMP=%VSETEMP%\%CURVSEVER%
IF NOT EXIST "%McAfeeClientsTemp%" MKDIR "%McAfeeClientsTemp%" > NUL: 2>&1
IF NOT EXIST "%VSETEMP%" MKDIR "%VSETEMP%" > NUL: 2>&1
IF NOT EXIST "%VSEVERTEMP%" MKDIR "%VSEVERTEMP%" > NUL: 2>&1

REM WRITE LOG BEFORE SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	BEFORE VSE INSTALL >> %MCAFEELOG%
CALL "%SUBSCRIPTS%\WriteLog-VSESUM.cmd" %MCAFEELOG%

REM COPY FILES LOCAL
COPY /Y %VSEINST% %VSEVERTEMP%\

CLS
ECHO.
ECHO	[INSTALLING VSE CLIENT %CURVSEVER%]
ECHO.
ECHO	Please Wait...

REM Install VSE Agent 
"%VSEVERTEMP%\SetupVSE.exe" /q

REM CHECK SUCCESS
CALL %CHECKVERSIONS%
IF "%VSEVER%"=="%CURVSEVER%" GOTO COMPLETED
GOTO ERROR
REM IF NOT EXIST "%EPOPATH%\FrameworkService.exe" GOTO ERROR

:COMPLETED
REM CLEANUP
IF EXIST "%VSEVERTEMP%\SetupVSE.exe" DEL /Q "%VSEVERTEMP%\*"

REM WRITE LOG AFTER SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	AFTER VSE CLIENT INSTALL >> %MCAFEELOG%
CALL "%SUBSCRIPTS%\WriteLog-VSESUM.cmd" %MCAFEELOG%
ECHO.	>> %MCAFEELOG%
ECHO	[VSE CLIENT %CURVSEVER% INSTALLED SUCCESSFULLY] (%DATE% %TIME%) >> %MCAFEELOG%

CLS
ECHO.
ECHO	[VSE CLIENT %CURVSEVER% INSTALLED SUCCESSFULLY]
ECHO.
ECHO.
SET ERROR=0
REM Wait 2 seconds
CALL %SUBSCRIPTS%\WAIT.cmd 2
GOTO END

:ERROR
COLOR 1c
CLS
ECHO.
ECHO	ERROR: [VSE CLIENT %CURVSEVER% INSTALL FAILED]
ECHO.
SET ERROR=1
REM WRITE LOG FAILED SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	ERROR: [VSE CLIENT INSTALL FAILED] (%DATE% %TIME%) >> %MCAFEELOG%
PAUSE
GOTO END

:END