@ECHO OFF
COLOR 1f

REM	-------------------------------------------------------------------------------
REM	+CHANGE LOG
REM	11/23/2011: Created
REM	12/06/2011: Added Checkversions and install check
REM	-------------------------------------------------------------------------------	

REM	-------------------------------------------------------------------------------
REM		AUTHOR: Levon Becker 
REM		TITLE: Uninstall-EPOAgent 
REM		PURPOSE: Uninstall McAfee EPO Agent
REM		VERSION: 1.1
REM	-------------------------------------------------------------------------------

CLS
CALL %CHECKVERSIONS%
REM CHECK IF EPO INSTALLED
REM IF EXIST "%EPOPATH%\FrmInst.exe" GOTO EPOUNINSTALL
IF NOT "%EPOVER%"=="Not Installed" GOTO EPOUNINSTALL
ECHO.
ECHO	[EPO AGENT NOT INSTALLED]
ECHO.
ECHO.	>> %MCAFEELOG%
ECHO	[EPO AGENT NOT INSTALLED] (%DATE% %TIME%) >> %MCAFEELOG%

ECHO.	>> %MCAFEELOG%
CALL %SUBSCRIPTS%\WAIT.cmd 2
GOTO END

:EPOUNINSTALL
REM REM WRITE LOG BEFORE SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	BEFORE EPO UNINSTALL >> %MCAFEELOG%
CALL "%SUBSCRIPTS%\WriteLog-EPOSUM.cmd" %MCAFEELOG%
ECHO.	>> %MCAFEELOG%
ECHO	[ATTEMPTING EPO AGENT UNINSTALL] (%DATE% %TIME%) >> %MCAFEELOG%


REM STOP SERVICES
ECHO	[STOPPING SERVICES]
ECHO.
REM STOP EPO SERVICE
NET STOP McAfeeFramework

CLS
ECHO.
ECHO	[UNINSTALLING EPO AGENT]
ECHO.
ECHO	Please Wait...
REM RUN SILENT INSTALL
"%EPOPATH%\FrmInst.exe" /forceuninstall /silent

REM CHECK SUCCESS
CALL %CHECKVERSIONS%
IF "%EPOVER%"=="Not Installed" GOTO COMPLETED
GOTO ERROR
REM IF EXIST "%EPOPATH%\FrmInst.exe" GOTO ERROR

:COMPLETED
REM WRITE LOG AFTER SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	AFTER EPO UNINSTALL >> %MCAFEELOG%
CALL "%SUBSCRIPTS%\WriteLog-EPOSUM.cmd" %MCAFEELOG%
ECHO.	>> %MCAFEELOG%
ECHO	[EPO AGENT UNINSTALLED SUCCESSFULLY] (%DATE% %TIME%) >> %MCAFEELOG%

REM Wait 4 seconds
CALL %SUBSCRIPTS%\WAIT.cmd 4

CLS
ECHO.
ECHO	[EPO AGENT UNINSTALLED SUCCESSFULLY]
ECHO.
SET ERROR=0
REM Wait 2 seconds
CALL %SUBSCRIPTS%\WAIT.cmd 2
GOTO END

:ERROR
COLOR 1c
CLS
ECHO.
ECHO	ERROR: [EPO AGENT UNINSTALL FAILED]
ECHO.
SET ERROR=1
REM WRITE LOG FAILED SUMMARY
ECHO.	>> %MCAFEELOG%
ECHO	ERROR: [EPO AGENT UNINSTALL FAILED] (%DATE% %TIME%) >> %MCAFEELOG%
REM CALL "%SUBSCRIPTS%\WriteLog-Summary.cmd" %MCAFEELOG%
PAUSE
GOTO END

:END

